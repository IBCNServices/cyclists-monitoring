REGISTER QUERY getQuantityObservationValue AS 

PREFIX f: <http://larkc.eu/csparql/sparql/jena/ext#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX schema: <http://schema.org/>

PREFIX cycling-sosa: <http://idlab.ugent.be/cycling-vocab/sosa#>
PREFIX cycling-profile: <http://idlab.ugent.be/cycling-vocab/profile#>

SELECT
    ?uuid
    ?firstName
    ?lastName
    ?sensor
    ?time
    ?value
FROM STREAM <http://idlab.ugent.be/cycling/stream> [RANGE 5s STEP 1s]
FROM <http://localhost:8177/cycling-riders.rdf>
FROM <http://localhost:8177/cycling-sensors.rdf>
WHERE { 
    ?o sosa:hasResult ?ov .
    ?o sosa:resultTime ?time .
    
    ?ov rdf:type cycling-sosa:QuantityObservationValue .
    ?ov schema:value ?value .
    
    {
        SELECT ?sensor ( MAX ( f:timestamp (?x, sosa:madeBySensor, ?sensor) ) AS ?ts ) 
        WHERE {
            ?x sosa:madeBySensor ?sensor .
            ?x sosa:hasResult ?xv .
            ?xv rdf:type cycling-sosa:QuantityObservationValue .
        }
        GROUP BY ?sensor
    }
    
    ?sensor sosa:isHostedBy ?device .
    ?device cycling-sosa:UUID ?uuid .
    ?athlete cycling-profile:monitoredBy ?device .
    ?athlete schema:givenName ?firstName .
    ?athlete schema:familyName ?lastName .
    
    FILTER ( f:timestamp (?o, sosa:madeBySensor, ?sensor) = ?ts )
}
